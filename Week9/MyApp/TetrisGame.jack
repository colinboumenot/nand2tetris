class TetrisGame {

   static TetrisGame game;
   static World world;
   field boolean gameOver;

   constructor TetrisGame new(){
     do World.setWorld();
     let world = World.getWorld();
     do Tetrominos.newWorld(world);
     let gameOver = false;
     return this;
   }

   method void dispose(){
     do Memory.deAlloc(this);
     return;
   }

   function void newGame(){
     let game = TetrisGame.new();
     return;
   }

   function TetrisGame getGame(){
     return game;
   }

   method void run(){
     var Tetrominos block;
     var Tetrominos nextBlock;
     var char currentKey;
     let nextBlock = Tetrominos.spawn(Random.randomRange(6));

     while(~gameOver){
       if (block = 0){
         let block = nextBlock;
         let nextBlock = Tetrominos.spawn(Random.randomRange(6));
         if (block.isGameOver()){
           let gameOver = true;
           do Screen.setColor(false);
           do Screen.drawRectangle(0,0,511,255);
           do Screen.setColor(true);
           return;
         }
       }
       do Sys.wait(100);
       let currentKey = Keyboard.keyPressed();
       if (currentKey = 130){
         if (block > 0){
           do block.moveLeft();
         }
       }
       if (currentKey = 132){
         if (block > 0){
           do block.moveRight();
         }
       }
       if (currentKey = 133){
         if (block > 0){
           let block = block.moveDown();
         }
       }
     }
     return;
   }
}

